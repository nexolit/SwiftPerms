plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "net.raphimc.class-token-replacer" version "1.1.5"
}

group = 'me.nexolit.ezperms'
version = '1.0.0'
ext.source = project.hasProperty('source') ? project.getProperty('source') : "default"

println "Building jar for: ${project.ext.source}"

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'jitpack'
        url 'https://jitpack.io'
    }
}

dependencies {
    shadow 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'
    shadow 'com.github.MilkBowl:VaultAPI:1.7'
    implementation 'org.bstats:bstats-bukkit:3.0.3'
}

sourceSets {
    main {
        classTokenReplacer {
            property("\${source}", project.ext.source)
        }
    }
}

tasks.named('shadowJar').configure {
    from(tasks.named("replaceTokens"))
    
    archiveClassifier.set(project.ext.source)
    relocate 'org.bstats', 'me.nexolit'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}